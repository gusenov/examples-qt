import QtQuick 2.12
import QtQuick.Window 2.12

Window {
    visible: true
    width: 480
    height: 200
    title: qsTr("Flow")

    // Размещение в виде потока (элемент Flow) упорядочивает элементы «Змейкой»,
    // которая пытается разместить как можно большее количество элементов в заданной области окна.
    Flow {
        anchors.fill: parent

        // Устанавливаем равные 20 пикселам отступы (anchors.margins) от всех
        // сторон главного окна приложения и промежутки между элементами (spacing) также
        // в 20 пикселов:
        anchors.margins: 20
        spacing: 20

        Repeater {
            // Элемент-повторитель содержит встроенную модель (model), которая возвращает 10 элементов
            // строк с цветовыми значениями, причем элемент с четным индексом получает значение
            // красного цвета «red», а с нечетным - значение зеленого цвета «green».
            model: {
                var v = new Array(10) ;
                for (var i = 0; i < v.length; ++i)
                    v[i] = i % 2 ? "green" : "red"
                return v;
            }

            Rectangle {
                width: 64
                height: 64

                // Для того чтобы
                // отобразить элементом Rectangle не прямоугольник, а окружность, мы задаем радиус закругления
                // прямоугольника (radius), равный половине одной из его сторон, то есть 32 пиксела.
                radius: 32

                // Основной цвет задается в прямоугольнике посредством текущего значения модели
                // данных (modelData).
                color: modelData

                Text {
                    color: "white"
                    font.pixelSize: 48
                    font. family: "Courier"
                    anchors.centerIn: parent

                    // Цифровое значение отображается при помощи дочернего элемента
                    // Text, в котором само значение является текущим индексом элемента (index) встроенной
                    // модели.
                    text: index
                }
            }
        }
    }
}
